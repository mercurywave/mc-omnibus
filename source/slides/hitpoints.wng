using Dom

library Slide

    ::RecordHP( => slide)
        slide : $Create(lambda(stage => async))
            await $HP.DoRecordHP(stage)

    ::SetInitialHP( => slide)
        slide : $Create(lambda(stage => async))
            await $HP.DoSetInitialHP(stage)

    library HP
        ::DoRecordHP(stage => async)
            @pipe : stage.ct.$Canceler.MakePipe()

            @container
            @pop : dim $Popup.Create("Persistent Damage")
                mixin $AppendChild(dim $Paragraph.Create())
                    mixin $SetHtml($MarkdownToHtml($GTW(textdata)))
                        Record each players final hit points (up to their printed hit points):

                mixin $AppendChild(dim $Div.Create() >> container)
                    mixin $Style.AddClasses("hpRecord")

            @fields : dim()
            for @idx in stage.state.player
                @hero : $Heroes.LookupByName(stage.state.player[idx].hero)
                @lastHp : stage.ghost["player",idx,"hp"] ? hero.$Heroes.GetHp()
                @cell : container.$AppendChild(dim $Div.Create())
                    mixin $Style.AddClasses("heroHp")
                    mixin $AppendChild(hero.$Heroes.MakeHeroBadge())
                    mixin $AppendChild(dim $NumberEntry.Create() >> fields[idx])
                        mixin $NumberEntry.SetText(lastHp)
                        mixin $NumberEntry.DoSelectAllOnFocus()

            @tryAdvance : lambda
                if !fields.$List.Any(`it.$NumberEntry.GetText() = ""`)
                    pipe.$Pipe.Kill()
            
            pop.$Popup.AddBotRightButton(tryAdvance)

            $Slide.HookAdvance(stage, tryAdvance)

            await pipe.$Pipe.ReadAsync()
            if stage.ct.$Canceler.IsLive()
                for @elem of fields at @idx
                    stage.delta["player",idx,"hp"] : $Math.Floor(elem.$NumberEntry.GetText())

        ::DoSetInitialHP(stage => async)
            @pipe : stage.ct.$Canceler.MakePipe()

            @container
            @pop : dim $Popup.Create("Persistent Damage")
                mixin $AppendChild(dim $Paragraph.Create())
                    mixin $SetHtml($GTW(textdata))
                        Set each player's starting hit points for the scenario to the following

                mixin $AppendChild(dim $Div.Create() >> container)
                    mixin $Style.AddClasses("hpRecord")

            for @idx in stage.state.player
                @hero : $Heroes.LookupByName(stage.state.player[idx].hero)
                @lastHp : stage.state["player",idx,"hp"]
                @cell : container.$AppendChild(dim $Div.Create())
                    mixin $Style.AddClasses("heroHp")
                    mixin $AppendChild(hero.$Heroes.MakeHeroBadge())
                    mixin $AppendChild(dim $Paragraph.Create("" + ~lastHp))
                        mixin $Style.AddClasses("hp")
            
            pop.$Popup.AddBotRightButton(`pipe.$Pipe.Kill()`)
            $Slide.HookAdvance(stage, `pipe.$Pipe.Kill()`)

            await pipe.$Pipe.ReadAsync()
